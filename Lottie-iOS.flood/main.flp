import Web
import JSON

execute("composition.flp")
execute("assets.flp")
execute("fonts.flp")
execute("Layer/layers.flp")
execute("Layer/groupLayer.flp")
execute("Layer/shapeLayer.flp")
execute("Layer/imageLayer.flp")
execute("Layer/textLayer.flp")
execute("animation.flp")
execute("keyvalues.flp")
execute("transform.flp")
execute("dashPattern.flp")
execute("propertyValue.flp")
execute("time.flp")
execute("effects.flp")
execute("hierarchy.flp")
execute("contentMode.flp")
execute("properties.flp")
execute("visibility.flp")
execute("pathAnimation.flp")
execute("characters.flp")

for timeline in timelines
    // Get the name of the timeline folder
    var timelineFolder = folderTable.symbolForID(timeline.id, "\(outputFolderPath)/\(documentFolderName)/\(templateFolderName)").name

    // Get the timeline output path
    var timelineOutputPath = "\(documentFolderName)/\(templateFolderName)/\(timelineFolder)"

    // Set the name for the html & css files to the name of the timeline
    var timelineName = timelineFolder

    // Folder names
    var imageFolderName = "img"
    var fontFolderName = "fonts"

    // The paths to use when referencing image and font assets
    var imageFolderPath = "\(timelineOutputPath)/\(imageFolderName)"
    var fontFolderPath = "\(timelineOutputPath)/\(fontFolderName)"

    // Delete previously exported project
    rm(timelineOutputPath)

    // Create output directories
    mkdir("\(imageFolderPath)")
    mkdir("\(fontFolderPath)")


    // Copy Images over
    var imagesInTimeline = document.images(forTimeline: timeline)
    for image in imagesInTimeline
        var imageData = image.serializeAsPNGData()
        if imageData != nil 
            write(data: imageData, to: "\(imageFolderPath)/\(image.name).png")
        end
    end

    // Copy Fonts
    for font in document.fonts(forTimeline: timeline)
        if !Web.webFonts.contains(font.name) && font.path != nil
            copy(from: font.path, to: "\(fontFolderPath)/\(font.familyName).\(font.extension)")
        end
    end

    // Create Lottie JSON String 
    var dictionary = lottieDictionaryFor(timeline: timeline, imageFolderName: imageFolderName)
    var jsonString = JSON.json(from: dictionary, prioritizedEntries: ["ty"])

    // make name of json file snake cased 
    // because asset files in android studio can only have lowercase a-z, 0-9, or underscore
    var jsonFileName = timelineName.snakeCased()

    // Create Lottie files
    
    // javascript file that exposes lottie animation as java script variable for use in index.html 
    process("timeline.js.flt", into: "\(timelineOutputPath)/\(timelineName).js", indentation: 0)
    
    // human-readable Lottie json file
    process("timeline.json.flt", into: "\(timelineOutputPath)/\(jsonFileName).json", indentation: 0)

    // minified version of Lottie json file to be used in production (smaller file size)
    process("timeline.min.json.flt", into: "\(timelineOutputPath)/\(jsonFileName).min.json", indentation: 0)    

    // html file used to quickly preview the Lottie animation
    process("index.html.flt", into: "\(timelineOutputPath)/index.html", indentation: 0)

    // Generate README
    var escape = true
    process("README.html.flt", into: "\(timelineOutputPath)/README.html", indentation: 0)
    escape = false

    // Clear the symbol table 
    symbolTable.removeAllSymbols()
end